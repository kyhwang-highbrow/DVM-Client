import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.perplelab.dragonvillagem.kr"
        minSdkVersion 16
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 11
        versionName "0.2.6"

        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    targets 'cocos2dlua'

                    // NDK r10e 사용
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'

                    // Android-21 미만 기기에서 실행하기 위함
                    arguments 'APP_PLATFORM=android-16'

                    def module_paths = [project.file("../../../cocos2d-x").absolutePath,
                                        project.file("../../../cocos2d-x/cocos").absolutePath,
                                        project.file("../../../cocos2d-x/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }
                    arguments '-j' + Runtime.runtime.availableProcessors()
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    sourceSets.main {
        java.srcDir "../../proj.android/src"
        res.srcDir "./res"
        assets.srcDir "../../proj.android/assets"
        manifest.srcFile "./AndroidManifest.xml"
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "../../proj.android/jni/Android.mk"
            }
        }
    }

    signingConfigs {
        release {
            if (project.hasProperty("PROP_UPLOAD_RELEASE_KEY") && PROP_UPLOAD_RELEASE_KEY.compareTo('1') ) {
                storeFile file(UPLOAD_RELEASE_STORE_FILE)
                storePassword UPLOAD_RELEASE_STORE_PASSWORD
                keyAlias UPLOAD_RELEASE_KEY_ALIAS
                keyPassword UPLOAD_RELEASE_KEY_PASSWORD
            } else {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
        debug {
            if (project.hasProperty("PROP_UPLOAD_RELEASE_KEY") && PROP_UPLOAD_RELEASE_KEY.compareTo('1') ) {
                storeFile file(UPLOAD_RELEASE_STORE_FILE)
                storePassword UPLOAD_RELEASE_STORE_PASSWORD
                keyAlias UPLOAD_RELEASE_KEY_ALIAS
                keyPassword UPLOAD_RELEASE_KEY_PASSWORD
            } else {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }
        debug {
            signingConfig signingConfigs.debug

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libcocos2dx')
    compile project(':downloader_library')

    // @firebase
    compile 'com.google.firebase:firebase-core:11.0.2'
    compile 'com.google.firebase:firebase-auth:11.0.2'
    compile 'com.google.firebase:firebase-messaging:11.0.2'

    // @google
    compile 'com.google.android.gms:play-services-auth:11.0.2'
    compile 'com.google.android.gms:play-services-games:11.0.2'

    // @facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
}

// @firebase
apply plugin: 'com.google.gms.google-services'

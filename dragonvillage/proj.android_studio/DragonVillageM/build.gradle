import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'


// task preBuild excuted before android build 
preBuild {
    println 'do it before build'
}

// task android build
android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.perplelab.dragonvillagem.kr"
        minSdkVersion 16
        targetSdkVersion PROP_TARGET_SDK_VERSION
        versionCode 13
        versionName "0.3.2"

        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    targets 'cocos2dlua'

                    // NDK r1b 사용
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'

                    // Android-21 미만 기기에서 실행하기 위함
                    arguments 'APP_PLATFORM=android-16'

                    def module_paths = [project.file("../../../cocos2d-x").absolutePath,
                                        project.file("../../../cocos2d-x/cocos").absolutePath,
                                        project.file("../../../cocos2d-x/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }
                    arguments '-j' + Runtime.runtime.availableProcessors()
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    // productFlavors or buildType 에 따른 개별적인 source 설정 가능
    sourceSets{
        full {
            assets.srcDir "../../../assets/full"
        }

        market {
            assets.srcDir "../../../assets/100mb"                  
        }

        main {
            java.srcDir "./src"
            res.srcDir "./res"
            manifest.srcFile "./AndroidManifest.xml"         
        }
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "../jni/Android.mk"
            }
        }
    }

    signingConfigs {
        // live 용 릴리즈키
        live_key {
            storeFile file(UPLOAD_RELEASE_STORE_FILE)
            storePassword UPLOAD_RELEASE_STORE_PASSWORD
            keyAlias UPLOAD_RELEASE_KEY_ALIAS
            keyPassword UPLOAD_RELEASE_KEY_PASSWORD
        }
        // test 용 릴리즈키
        test_key {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    /**
    * The productFlavors {} block is where you can configure multiple product
    * flavors. This allows you to create different versions of your app that can
    * override defaultConfig {} with their own settings. Product flavors are
    * optional, and the build system does not create them by default.
    */
    productFlavors {
        // 100mb under assets 사용
        market {
            // applicationId "com.perplelab.dragonvillagem.market.kr" ?
            // applicationIdSuffix ".market"
            // versionNameSuffix "-market"
            signingConfig signingConfigs.live_key
        }

        // full assests 사용
        full {
            // applicationIdSuffix ".full"
            // versionNameSuffix "-full"
            signingConfig signingConfigs.test_key
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            // signingConfig initialize
            signingConfig null

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                }
            }
        }
        debug {
            // signingConfig initialize
            signingConfig null

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                }
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libcocos2dx')

    // @unity-ads
    compile(name:'unity-ads', ext:'aar')

    // @apk-expansion
    compile project(':downloader_library')

    // @firebase
    compile 'com.google.firebase:firebase-core:11.0.2'
    compile 'com.google.firebase:firebase-auth:11.0.2'
    compile 'com.google.firebase:firebase-messaging:11.0.2'

    // @google
    compile 'com.google.android.gms:play-services-auth:11.0.2'
    compile 'com.google.android.gms:play-services-games:11.0.2'

    // @facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
}

// @firebase
apply plugin: 'com.google.gms.google-services'

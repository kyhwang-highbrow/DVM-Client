import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'


// task preBuild excuted before android build 
preBuild {
    println '[TASK preBuild] do it before build'
}

// task android build
android {
    compileSdkVersion 25
    buildToolsVersion "26.0.1"

    defaultConfig {
        applicationId "com.perplelab.dragonvillagem.kr"
        minSdkVersion 16
        targetSdkVersion PROP_TARGET_SDK_VERSION
        
        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    targets 'cocos2dlua'

                    // NDK r1b 사용
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'

                    // Android-21 미만 기기에서 실행하기 위함
                    arguments 'APP_PLATFORM=android-16'

                    // cpp flags
                    cppFlags.addAll(PROP_CPP_FLAGS.split(':').collect { it as String })

                    // module path
                    def module_paths = [project.file("../../../cocos2d-x").absolutePath,
                                        project.file("../../../cocos2d-x/cocos").absolutePath,
                                        project.file("../../../cocos2d-x/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }

                    // processors
                    arguments '-j' + Runtime.runtime.availableProcessors()

                    // abi
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    // productFlavors or buildType 에 따른 개별적인 source 설정 가능
    sourceSets{
        dev100mbTest{
            assets.srcDir "../../../assets/100mb"
        }
        devFullTest{
            assets.srcDir "../../../assets/full"
        }

        qa100mbTest{
            assets.srcDir "../../../assets/100mb"
        }
        qaFullTest{
            assets.srcDir "../../../assets/full"
        }

        live100mb{
            assets.srcDir "../../../assets/100mb"
        }
        liveFull{
            assets.srcDir "../../../assets/full"
        }

        main {
            java.srcDir "./src"
            res.srcDir "./res"
            manifest.srcFile "./AndroidManifest.xml"         
        }
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "../jni/Android.mk"
            }
        }
    }

    signingConfigs {
        // live 용 릴리즈키
        upload_keystore {
            storeFile file(UPLOAD_RELEASE_STORE_FILE)
            storePassword UPLOAD_RELEASE_STORE_PASSWORD
            keyAlias UPLOAD_RELEASE_KEY_ALIAS
            keyPassword UPLOAD_RELEASE_KEY_PASSWORD
        }
        // test 용 릴리즈키
        keystore {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
    }

    /**
    * The productFlavors {} block is where you can configure multiple product
    * flavors. This allows you to create different versions of your app that can
    * override defaultConfig {} with their own settings. Product flavors are
    * optional, and the build system does not create them by default.
    */
    productFlavors {
        // DEV
        dev100mbTest{
			versionCode 1
			versionName "9.9.9"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_DEV")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=true")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        devFullTest{
			versionCode 1
			versionName "9.9.9"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_DEV")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=false")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }

        // QA
        qa100mbTest{
			versionCode 14
			versionName "0.3.4"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_QA")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=true")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        qaFullTest{
			versionCode 14
			versionName "0.3.4"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_QA")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=false")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }

        // LIVE
        live100mb{
			versionCode 14
			versionName "1.0.0"
            signingConfig signingConfigs.upload_keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_LIVE")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=true")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=false")
                }
            }
        }
        liveFull{
			versionCode 14
			versionName "1.0.0"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_LIVE")
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=false")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=false")
                }
            }
        }
		
		/* dimension 활용하여 다음과 같이 구성하는 것도 고민중
		dev{
			dimension "server"
		}
		qa{
			dimension "server"
		}
		live{
			dimension "server"
		}
		
		100mb{
			dimension "assets"
		}
		market{
			dimension "assets"
		}
		
		test{
			dimension "mode"
		}
		normal{
			dimension "mode"
		}
		*/
    }

    buildTypes {
        release {
            // signingConfig initialize
            signingConfig null

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                    cppFlags.add("-DNDEBUG")
                }
            }
        }
        debug {
            // signingConfig initialize
            signingConfig null

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                    cppFlags.add("-DCOCOS2D_DEBUG=1")
                }
            }
        }
    }
}

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs 'libs'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':libcocos2dx')

    // @unity-ads
    compile(name:'unity-ads', ext:'aar')

    // @apk-expansion
    compile project(':downloader_library')

    // @firebase
    compile 'com.google.firebase:firebase-core:11.0.2'
    compile 'com.google.firebase:firebase-auth:11.0.2'
    compile 'com.google.firebase:firebase-messaging:11.0.2'

    // @google
    compile 'com.google.android.gms:play-services-auth:11.0.2'
    compile 'com.google.android.gms:play-services-games:11.0.2'

    // @facebook
    compile 'com.facebook.android:facebook-android-sdk:[4,5)'
}

// @firebase
apply plugin: 'com.google.gms.google-services'

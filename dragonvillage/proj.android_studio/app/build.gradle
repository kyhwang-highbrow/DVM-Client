import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'

// task preBuild excuted before android build 
preBuild {
    println '[task preBuild] do it before build'
    println PROP_PACKAGE_NAME
    println PROP_VERSION_CODE
    println PROP_APPVER_DEV
    println PROP_APPVER_QA
    println PROP_APPVER_LIVE
}

// task android build
android {
    compileSdkVersion 27
    buildToolsVersion "26.0.2"

    defaultConfig {
        applicationId PROP_PACKAGE_NAME
		versionCode PROP_VERSION_CODE.toInteger()
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION

        multiDexEnabled true

        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    targets 'cocos2dlua'

                    // NDK r14b 사용
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'

                    // Android-21 미만 기기에서 실행하기 위함
                    arguments 'APP_PLATFORM=android-16'

                    // cpp flags
                    cppFlags.addAll(PROP_CPP_FLAGS.split(':').collect { it as String })

                    // module path
                    def module_paths = [project.file("../../../cocos2d-x").absolutePath,
                                        project.file("../../../cocos2d-x/cocos").absolutePath,
                                        project.file("../../../cocos2d-x/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }

                    // processors
                    arguments '-j' + Runtime.runtime.availableProcessors()

                    // abi
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    // productFlavors or buildType 에 따른 개별적인 source 설정 가능
    // favors + buildType 가능
    sourceSets{
        market{
            assets.srcDir "../../../assets/100mb"
        }
        full{
            assets.srcDir "../../../assets/full"
        }
        main {
            java.srcDir "./src"
            res.srcDir "./res"
            manifest.srcFile "./AndroidManifest.xml"         
        }
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "../jni/Android.mk"
            }
        }
    }

    signingConfigs {
        // live upload 용 릴리즈키
        upload_keystore {
            storeFile file(UPLOAD_RELEASE_STORE_FILE)
            storePassword UPLOAD_RELEASE_STORE_PASSWORD
            keyAlias UPLOAD_RELEASE_KEY_ALIAS
            keyPassword UPLOAD_RELEASE_KEY_PASSWORD
        }
        // test 용 릴리즈키
        keystore {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        // 디버그키
        debug_keystore {
            storeFile file(DEBUG_STORE_FILE)
            storePassword DEBUG_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }
    }

    // Specifies the flavor dimensions you want to use. The order in which you
    // list each dimension determines its priority, from highest to lowest,
    // when Gradle merges variant sources and configurations. You must assign
    // each product flavor you configure to one of the flavor dimensions.

    flavorDimensions "server", "asset"

    /**
    * The productFlavors {} block is where you can configure multiple product
    * flavors. This allows you to create different versions of your app that can
    * override defaultConfig {} with their own settings. Product flavors are
    * optional, and the build system does not create them by default.
    */
    productFlavors {
        // dimension server
        dev{
            dimension "server"
            versionName PROP_APPVER_DEV
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_DEV")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        qa{
            dimension "server"
            versionName PROP_APPVER_QA
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_QA")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        live{
            dimension "server"
            versionName PROP_APPVER_LIVE
            signingConfig signingConfigs.upload_keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_LIVE")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=false")
                }
            }
        }

        //dimension asset
        market{
            dimension "asset"
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=true")
                }
            }
        }
        full{
            dimension "asset"
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=false")
                }
            }
        }

    }

    buildTypes {
        release {
            // signingConfig initialize
            signingConfig null

            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-project.txt'

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                    cppFlags.add("-DNDEBUG")
                }
            }
        }
        debug {
            // signingConfig initialize
            signingConfig null

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                    cppFlags.add("-DCOCOS2D_DEBUG=1")
                }
            }
        }
    }

    configurations {
        all*.exclude group: 'com.android.support', module: 'support-annotations'
    }

    // apk 이름 변경 : "dvm_version_vc00_server_asset_buildtype.apk
    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavors = variant.productFlavors

            def server_flavor = flavors[0].name // 여기서 0은 dimension 순서를 의미
            def asset_flaver = flavors[1].name // market or full
            def version = variant.versionName
            def versionCode = defaultConfig.versionCode
            def buildType = variant.variantData.variantConfiguration.buildType.name

            outputFileName = "dvm_${version}_vc${versionCode}_${server_flavor}_${asset_flaver}_${buildType}.apk"
        }
    }


} // end:android

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs "libs"
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // @cocos2d-x
    implementation project(':libcocos2dx')

    // @apk-expansion
    implementation project(':downloader_library')

    // @multidex below Android 6.0
    implementation 'com.android.support:multidex:1.0.2'

    // @perplesdk
    implementation(name:'perplesdk', ext:'aar')

    // @adcolony
    implementation 'com.adcolony:sdk:3.3.0'
    implementation 'com.google.android.gms:play-services-ads:11.8.0'

    // @unity-ads
    implementation(name:'unity-ads', ext:'aar')

    // @naver-cafe
    implementation(name: 'cafeSdk-3.2.0', ext: 'aar')

    implementation 'com.navercorp.volleyextensions:volleyer:2.0.1', {
        exclude group: 'com.mcxiaoke.volley', module: 'library'
    }
    implementation 'com.google.code.gson:gson:2.8.0'
    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.squareup:otto:1.3.8'

    // @firebase
    implementation 'com.google.firebase:firebase-core:11.8.0'
    implementation 'com.google.firebase:firebase-auth:11.8.0'
    implementation 'com.google.firebase:firebase-messaging:11.8.0'

    // @google
    implementation 'com.google.android.gms:play-services-auth:11.8.0'
    implementation 'com.google.android.gms:play-services-games:11.8.0'

    // @facebook
    implementation 'com.facebook.android:facebook-android-sdk:4.30.0'

    // @twitter
    implementation 'com.twitter.sdk.android:twitter:3.1.1'

    // @igawCommon
    implementation 'com.android.installreferrer:installreferrer:1.0'

    // @adjust
    implementation 'com.adjust.sdk:adjust-android:4.12.0'
    implementation 'com.google.android.gms:play-services-analytics:11.8.0'
}

// @firebase
apply plugin: 'com.google.gms.google-services'

import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
// @crashlytics
apply plugin: 'io.fabric'

// task preBuild excuted before android build 
preBuild {
    println '[task preBuild] do it before build'
    println "Package name : " + PROP_PACKAGE_NAME
    println "Version code : " + PROP_VERSION_CODE
    println "App version DEV : " + PROP_APPVER_DEV
    println "App version QA : " + PROP_APPVER_QA
    println "App version LIVE : " + PROP_APPVER_LIVE
}

// task android build
android {
    compileSdkVersion PROP_COMPILE_SDK_VERSION.toInteger()
    buildToolsVersion PROP_BUILD_TOOLS_VERSION

    defaultConfig {
        applicationId PROP_PACKAGE_NAME
		versionCode PROP_VERSION_CODE.toInteger()
        minSdkVersion PROP_MIN_SDK_VERSION
        targetSdkVersion PROP_TARGET_SDK_VERSION

        multiDexEnabled true

        // buildConfig.java에 저장할 초기값 세팅
        buildConfigField "String", "PUBLIC_KEY", ""

        externalNativeBuild {
            ndkBuild {
                if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                    targets 'cocos2dlua'

                    // NDK r14b 사용
                    arguments 'NDK_TOOLCHAIN_VERSION=4.9'

                    // Android-21 미만 기기에서 실행하기 위함
                    arguments 'APP_PLATFORM=android-16'

                    // cpp flags
                    cppFlags.addAll(PROP_CPP_FLAGS.split(':').collect { it as String })

                    // module path
                    def module_paths = [project.file("../../../cocos2d-x").absolutePath,
                                        project.file("../../../cocos2d-x/cocos").absolutePath,
                                        project.file("../../../cocos2d-x/external").absolutePath]
                    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
                        // should use '/'
                        module_paths = module_paths.collect { it.replaceAll('\\\\', '/') }
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(";")
                    } else {
                        arguments 'NDK_MODULE_PATH=' + module_paths.join(':')
                    }

                    // processors
                    arguments '-j' + Runtime.runtime.availableProcessors()

                    // abi
                    abiFilters.addAll(PROP_APP_ABI.split(':').collect { it as String })
                }
            }
        }
    }

    // productFlavors or buildType 에 따른 개별적인 source 설정 가능
    // favors + buildType 가능
    sourceSets{
        dev {
            res.srcDirs = ["res/common", "res/dev"]
        }
        qa {
            res.srcDirs = ["res/common", "res/qa"]
        }
        liveqa {
            res.srcDirs = ["res/common", "res/liveqa"]
        }
        live {
            res.srcDirs = ["res/common", "res/live"]
        }
        

        market {
            assets.srcDir "../../../assets/100mb"
        }
        full {
            assets.srcDir "../../../assets/full"
        }

        google {
            java.srcDirs = ["src/main", "src/google"]
        }
        xsolla { 
            java.srcDirs = ["src/main", "src/xsolla"]
        }
        cafebazaar { 
            java.srcDirs = ["src/main", "src/cafebazaar"]
        }
        onestore {
            java.srcDirs = ["src/main", "src/google"]
        }


        main {
            manifest.srcFile "./AndroidManifest.xml"         
        }
    }

    externalNativeBuild {
        ndkBuild {
            if (!project.hasProperty("PROP_NDK_MODE") || PROP_NDK_MODE.compareTo('none') != 0) {
                // skip the NDK Build step if PROP_NDK_MODE is none
                path "../jni/Android.mk"
            }
        }
    }

    signingConfigs {
        // live upload 용 릴리즈키
        upload_keystore {
            storeFile file(UPLOAD_RELEASE_STORE_FILE)
            storePassword UPLOAD_RELEASE_STORE_PASSWORD
            keyAlias UPLOAD_RELEASE_KEY_ALIAS
            keyPassword UPLOAD_RELEASE_KEY_PASSWORD
        }
        // test 용 릴리즈키
        keystore {
            storeFile file(RELEASE_STORE_FILE)
            storePassword RELEASE_STORE_PASSWORD
            keyAlias RELEASE_KEY_ALIAS
            keyPassword RELEASE_KEY_PASSWORD
        }
        // 디버그키
        debug_keystore {
            storeFile file(DEBUG_STORE_FILE)
            storePassword DEBUG_STORE_PASSWORD
            keyAlias DEBUG_KEY_ALIAS
            keyPassword DEBUG_KEY_PASSWORD
        }
    }

    // Specifies the flavor dimensions you want to use. The order in which you
    // list each dimension determines its priority, from highest to lowest,
    // when Gradle merges variant sources and configurations. You must assign
    // each product flavor you configure to one of the flavor dimensions.

    flavorDimensions "server", "asset", "platform"

    /**
    * The productFlavors {} block is where you can configure multiple product
    * flavors. This allows you to create different versions of your app that can
    * override defaultConfig {} with their own settings. Product flavors are
    * optional, and the build system does not create them by default.
    */
    productFlavors {
        // dimension server
        dev{
            dimension "server"
            versionName PROP_APPVER_DEV
            applicationIdSuffix ".dev"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_DEV")
                    cppFlags.add("-DMAJOR_VER=NUM_0")
                    cppFlags.add("-DMINOR_VER=NUM_6")
                    cppFlags.add("-DBUILD_VER=NUM_9")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        qa{
            dimension "server"
            versionName PROP_APPVER_QA
            applicationIdSuffix ".kr"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_QA")
                    cppFlags.add("-DMAJOR_VER=NUM_0")
                    cppFlags.add("-DMINOR_VER=NUM_7")
                    cppFlags.add("-DBUILD_VER=NUM_0")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        liveqa{
            dimension "server"
            versionName PROP_APPVER_LIVE
            applicationIdSuffix ".qa"
            signingConfig signingConfigs.keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_QA")
                    cppFlags.add("-DMAJOR_VER=NUM_1")
                    cppFlags.add("-DMINOR_VER=NUM_2")
                    cppFlags.add("-DBUILD_VER=NUM_1")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=true")
                }
            }
        }
        live{
            dimension "server"
            versionName PROP_APPVER_LIVE
            applicationIdSuffix ".kr"
            signingConfig signingConfigs.upload_keystore
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DTARGET_SERVER=SERVER_LIVE")
                    cppFlags.add("-DMAJOR_VER=NUM_1")
                    cppFlags.add("-DMINOR_VER=NUM_2")
                    cppFlags.add("-DBUILD_VER=NUM_1")
                    cppFlags.add("-DUSE_LUA_EXT=false")
                    cppFlags.add("-DIS_TEST_MODE=false")
                    cppFlags.add("-DLUA_DEBUG=1")
                }
            }
        }

        //dimension asset
        market{
            dimension "asset"
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=true")
                }
            }
        }
        full{
            dimension "asset"
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DUSE_PATCH=true")
                    cppFlags.add("-DUSE_OBB=false")
                }
            }
        }

        // platform
        google {
            dimension "platform"
        }
        xsolla {
            dimension "platform"
        }
        cafebazaar {
            dimension "platform"
            externalNativeBuild {
                ndkBuild {
                    cppFlags.add("-DIS_CAFE_BAZAAR_BUILD=true")
                }
            }
        }
        onestore {
            dimension "platform"
            buildConfigField "String", "PUBLIC_KEY", "\"MIGfMA0GCSqGSIb3DQEBAQUAA4GNADCBiQKBgQCps9StRMqg4wDFchmRLqFdbc7d0xnBJrG4hrrJvwMu3GZIMsfi+wQV4z744sTu6KoD+QihdJwkS3fgHfF7QDcBFYM72JvG1+//5AXcAZtETuszP78ODXPiVrfjzfDSD6Zyy7Nvnb0XvGsZSg+EfIyYHerdFAU6tATSgFg6AXQZswIDAQAB\""
        }
        
    }

    buildTypes {
        release {
            // signingConfig initialize
            signingConfig null
            minifyEnabled false

            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt', 'proguard-project.txt'

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=0'
                    cppFlags.add("-DNDEBUG")
                }
            }
        }
        debug {
            // signingConfig initialize
            signingConfig null
            minifyEnabled false
            debuggable true

            externalNativeBuild {
                ndkBuild {
                    arguments 'NDK_DEBUG=1'
                    cppFlags.add("-DCOCOS2D_DEBUG=1")
                }
            }
        }
    }

    configurations {
        all*.exclude group: 'com.android.support', module: 'support-annotations'
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    
    // apk 이름 변경 : "dvm_version_vc00_server_asset_buildtype.apk
    applicationVariants.all { variant ->
        variant.outputs.all {
            def flavors = variant.productFlavors

            def server_flavor = flavors[0].name // 여기서 0은 dimension 순서를 의미
            def asset_flavor = flavors[1].name // market or full
            def platform_flavor = flavors[2].name // google or xsolla or cafebazaar or onestore
            def version = variant.versionName
            def versionCode = defaultConfig.versionCode
            def buildType = variant.variantData.variantConfiguration.buildType.name

            outputFileName = "dvm_${version}_vc${versionCode}_${server_flavor}_${asset_flavor}_${platform_flavor}_${buildType}.apk"
        }
    }
} // end:android

allprojects {
    repositories {
        jcenter()
        flatDir {
            dirs "libs"
        }
    }
    dependencies {
        // support Kotlin
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

        // @multidex below Android api 21 (5.0 lolipop)
        implementation 'androidx.multidex:multidex:2.0.1'

        // @cocos2d-x
        implementation project(':libcocos2dx')

        // @apk-expansion
        implementation project(':downloader_library')
    
        // perplesdk dependencies
        // @perplesdk
        implementation(name:'perplesdk', ext:'aar')

        // @naver-cafe
        implementation(name: 'cafeSdk-3.3.4', ext: 'aar')
        implementation 'com.navercorp.volleyextensions:volleyer:2.0.1', {
            exclude group: 'com.mcxiaoke.volley', module: 'library'
        }
        implementation 'com.google.code.gson:gson:2.8.5'
        implementation 'com.github.bumptech.glide:glide:4.9.0'
        implementation 'com.squareup:otto:1.3.8'

        // @firebase
        implementation 'com.google.firebase:firebase-analytics:17.2.0'
        implementation 'com.google.firebase:firebase-auth:19.0.0'
        implementation 'com.google.firebase:firebase-messaging:20.0.0'

        // @crashlytics
        implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'
        implementation 'com.crashlytics.sdk.android:crashlytics-ndk:2.1.0'

        // @google
        implementation 'com.google.android.gms:play-services-auth:17.0.0'
        implementation 'com.google.android.gms:play-services-games:18.0.1'
        implementation 'com.google.android.gms:play-services-analytics:17.0.0'

        // @facebook
        implementation 'com.facebook.android:facebook-android-sdk:5.5.1'

        // @twitter
        implementation 'com.twitter.sdk.android:twitter:3.3.0'

        // @igawCommon
        implementation 'com.android.installreferrer:installreferrer:1.0'

        // @tapjoy https://dev.tapjoy.com/ko/sdk-integration/
        implementation 'com.tapjoy:tapjoy-android-sdk:12.3.3'

        // @adjust
        implementation 'com.adjust.sdk:adjust-android:4.18.1'

        // @admob
        implementation 'com.google.firebase:firebase-ads:18.2.0'
        implementation 'androidx.annotation:annotation:1.1.0'
        implementation 'com.google.ads.mediation:adcolony:3.3.11.0'
        implementation 'com.google.ads.mediation:unity:3.2.0.1'
        implementation 'com.google.ads.mediation:tapjoy:12.3.3.0'
        // @adcolony
        implementation 'com.adcolony:sdk:3.3.5'
        // @unity-ads
        implementation 'com.unity3d.ads:unity-ads:2.3.0'
    }
}
// @firebase
apply plugin: 'com.google.gms.google-services'